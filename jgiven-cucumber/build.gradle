
description = "Module for converting Cucumber reports into JGiven reports "

dependencies {
    compile project(':jgiven-core')
    compile(group: 'junit', name: 'junit', version: junitVersion)
    testCompile project(':jgiven-junit')
    testCompile group: 'info.cukes', name: 'cucumber-junit', version: '1.2.0'
    testCompile 'info.cukes:cucumber-java:1.2.0'
}


configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['-p', 'json', '--glue', 'com.tngtech.jgiven.cucumber', 'src/test/resources']
        }
    }
}

task convertToJGiven() {
    dependsOn cucumber
    doLast {
        javaexec {
            main = 'com.tngtech.jgiven.cucumber.CucumberToJGivenConverter'
            classpath = configurations.compile + sourceSets.main.output
            args = ['--targetDir', 'build/reports/jgiven/json', 'build/cucumber-report.json']
        }
    }
}

task convertCompleteCucumberReportToJGiven() {
    dependsOn compileTestJava
    doLast {
        javaexec {
            main = 'com.tngtech.jgiven.cucumber.CucumberToJGivenConverter'
            classpath = configurations.compile + sourceSets.main.output
            args = ['--targetDir', 'build/reports/jgiven/json', 'src/test/resources/complete-cucumber-report.json']
        }
    }
}



cucumber.finalizedBy( convertToJGiven );
convertToJGiven.finalizedBy( jgivenHtml5Report );
convertCompleteCucumberReportToJGiven.finalizedBy( jgivenHtml5Report );


// the cucumber scenarios are failing on purpose
test.ignoreFailures = true


