buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0-alpha1'
    }
}
apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.1'
    defaultConfig {
        applicationId "com.tngtech.jgiven.android.example"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

configurations {
  jgivenReport
}

dependencies {
  compile 'com.android.support:appcompat-v7:25.0.0'

  androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })

  androidTestCompile(project(':jgiven-android')) {
      exclude group: "junit"
      //why does this cause double junit ActiveSuite issue beats me
      //exclude group: "com.google.android.tools"
  }

  jgivenReport project(':jgiven-html5-report')
  jgivenReport 'org.slf4j:slf4j-simple:'+slf4jVersion
}

task cleanJGivenReports(type: Delete) {
  delete 'build/reports/jgiven/json'
}

task pullJGivenReports(type: Exec, dependsOn: cleanJGivenReports) {
    file('build/reports/jgiven/json').mkdirs()

    def adb = android.getAdbExe().toString()
    commandLine adb, 'pull', '/storage/emulated/0/Download/jgiven-reports', 'build/reports/jgiven/json'
}

task cleanJGivenHtmlReport(type: Delete) {
  delete 'build/reports/jgiven/html'
}

task jgivenHtml5Report(type: JavaExec, dependsOn: cleanJGivenHtmlReport) {
    main = 'com.tngtech.jgiven.report.ReportGenerator'
    args '--sourceDir=build/reports/jgiven/json',
         '--targetDir=build/reports/jgiven/html',
         '--format=html5',
         '--exclude-empty-scenarios=true'
        classpath = configurations.jgivenReport
}

pullJGivenReports.finalizedBy(jgivenHtml5Report)

tasks.whenTaskAdded { task ->
  if (task.name == 'connectedAndroidTest') {
    task.finalizedBy 'pullJGivenReports'
  }
}


